#version 450

#define eps 0.1
#define damping (0.98)
#define MAX_VEL 5.0

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct Vertex
{
    vec2 pos;
    vec2 vel;
};

layout (push_constant) uniform PushConstants
{
    bool enabled;
    float timestep;
    vec2 attractor;
} pc;

layout (set = 0, binding = 0) buffer Data
{
    Vertex vertices[];
} data;

vec2 clamp_to_bounds(vec2 pos)
{
    return vec2(
        clamp(pos.x, -2.0, 2.0),
        clamp(pos.y, -1.0, 1.0)
    );
}

vec2 clamp_velocity(vec2 vel)
{
    if (dot(vel, vel) > MAX_VEL * MAX_VEL)
    {
        return normalize(vel) * MAX_VEL;
    }
    else
    {
        return vel;
    }
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    Vertex vertex = data.vertices[idx];

    if (pc.enabled)
    {
        vec2 diff = pc.attractor - vertex.pos;
        vec2 dir = normalize(diff);
        vec2 acceleration = dir / (dot(diff, diff) + eps);
        vertex.vel += acceleration * pc.timestep;
    }

    vertex.vel = clamp_velocity(vertex.vel);
    vertex.vel *= damping;
    vertex.pos += vertex.vel * pc.timestep;
    vertex.pos = clamp_to_bounds(vertex.pos);

    data.vertices[idx] = vertex;
}
